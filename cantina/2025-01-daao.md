## [H-01] Contributions aren't wrapped into WETH blocking the possibility to finalize a foundraising
### Summary
The contract has a critical issue where users contribute ETH (msg.value) directly, but the liquidity pool creation logic assumes the second token is WETH (wrapped ETH). This mismatch prevents the pool from being created thus finalizing foundraising is impossible.
### Finding Description
Users contribute ETH to the contract via msg.value.
The contract attempts to create a liquidity pool using daoToken and MODE (assumed to be WETH).
However, the contract does not wrap the contributed ETH into WETH before calling POSITION_MANAGER.mint.
Since POSITION_MANAGER.mint requires ERC-20 tokens (and ETH is not an ERC-20 token), the transaction will fail, and the liquidity pool will not be created.
### Impact Explanation
Failed Pool Creation: The contract will fail to create the liquidity pool, rendering the fundraising process incomplete, funds wouldn't be fully locked thanks to the `emergencyEscape`
### Recommendation
To resolve this issue, the contract should wrap the contributed ETH into WETH before attempting to create the liquidity pool.
```solidity
    IWETH(MODE).deposit{value: amountForLP}();
```

## [H-02] Swap callback can lead to stealing funds from any user due to a lack of validation of the msg.sender
### Summary
The function does not validate that msg.sender is a legitimate pool, which could allow a malicious contract to trick the contract into transferring tokens.
### Finding Description
The uniswapV3SwapCallback function assumes that msg.sender is a valid pool. However, there is no validation to ensure that msg.sender is indeed a legitimate pool deployed by the Uniswap V3 factory. Anyone could deploy a contract and trigger the uniswapV3SwapCallback.
### Impact Explanation
If an attacker deploys a malicious contract that mimics a Uniswap V3 pool and calls this function, they could steal tokens from the sender address. This could result in significant financial losses for users interacting with the contract, users that previously approved tokens.
### Recommendation
Add a callback verification computing the pool as done in velodrome slipstream periphery contracts (SwapRouter):
```solidity
    function uniswapV3SwapCallback(int256 amount0Delta, int256 amount1Delta, bytes calldata _data) external override {
        require(amount0Delta > 0 || amount1Delta > 0); // swaps entirely within 0-liquidity regions are not supported
        SwapCallbackData memory data = abi.decode(_data, (SwapCallbackData));
        (address tokenIn, address tokenOut, int24 tickSpacing) = data.path.decodeFirstPool();
        CallbackValidation.verifyCallback(factory, tokenIn, tokenOut, tickSpacing);
        ...
```

## [M-01] None Tier cannot have a tierLimit causing impossibility to contribute for None tier
### Summary
Daao allows whitelisted and not whitelisted users to participate. The contribution amount is limited by a tierLimits mapping but a value cannot be set for tier None which cause the users with None tier to not be able to participate
### Finding Description
`Daao::contribute` has a check for tier limits amount to check that previous contributions + msg.value remains below or equal each tier limit. Non whitelisted users are expected to contribute in cases where maxWhitelistAmount is 0.
```solidity
        // Teir allocation
        tierLimits[WhitelistTier.Platinum] = PLATINUM_DEFAULT_LIMIT;
        tierLimits[WhitelistTier.Gold] = GOLD_DEFAULT_LIMIT;
        tierLimits[WhitelistTier.Silver] = SILVER_DEFAULT_LIMIT;
```
the different tiers are
```solidity
    enum WhitelistTier {
        None,
        Platinum,
        Gold,
        Silver
    }
```
Tier `None` being the default value of the enum, which is the one for not whitelisted users. In `contribute` we check that the contributions of the user + msg.value doesn't exceed the user limit
```solidity
        uint256 userLimit = tierLimits[userTier];
        require(
            contributions[msg.sender] + msg.value <= userLimit,
            "Exceeding tier limit"
        );
```
But as seen, the tierLimits isn't set for WhitelistTier == None, thus the userLimit for a not whitelisted user will be 0, the default value, making it impossible for users to contribute.
Moreover, the updateTierLimit doesn't allow setting a limit for the Tier None, which makes it more than only a constructor issue, it will never be possible for non whitelisted users to pass an amount and then contribute.
```solidity
    function updateTierLimit(WhitelistTier _tier, uint256 _newLimit) external {
        require(
            msg.sender == owner() || msg.sender == protocolAdmin,
            "Not authorized"
        );
        require(_tier != WhitelistTier.None, "Invalid tier");
```
### Impact Explanation
The impact is high as it is expected that not whitelisted users can contribute which in fact will not be possible
### Likelihood Explanation
Likelihood is medium as it requires allowing not whitelisted users to contribute which can sometimes not be the case
### Proof of Concept
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import "../src/Daao.sol";

contract DaaoTest is Test {
    Daao public daao;
    address owner = address(0x1);
    address public protocolAdmin = makeAddr("protocolAdmin");
    address public whitelistedUser = makeAddr("whitelistedUser");
    address public nonWhitelistedUser = makeAddr("nonWhitelistedUser");
    uint256 fundraisingGoal = 10 ether;
    uint256 fundraisingDeadline = block.timestamp + 1 weeks;
    uint256 fundExpiry = block.timestamp + 2 weeks;
    // maxWhitelistAmount is set to 0 to allow all non whitelisted users to contribute
    uint256 maxWhitelistAmount = 0;
    uint256 maxPublicContributionAmount = 10 ether;

    function setUp() public {
        vm.prank(owner);
        // Deploy the Daao contract
        daao = new Daao(
            fundraisingGoal,
            "TestDAO",
            "TDAO",
            fundraisingDeadline,
            fundExpiry,
            owner,
            address(0x5), // liquidityLockerFactory
            maxWhitelistAmount,
            protocolAdmin,
            maxPublicContributionAmount
        );

        vm.deal(whitelistedUser, 10 ether);
        vm.deal(nonWhitelistedUser, 10 ether);
    }

    function testNonWhitelistedUserCannotContribute() public {
        // Attempt to contribute as a non-whitelisted user
        vm.prank(nonWhitelistedUser);
        // Going to revert if though if maxWhitelistAmount is set to 0
        vm.expectRevert("Exceeding tier limit");
        daao.contribute{value: 0.1 ether}();
    }
}
```
### Recommendation
- Make the tierLimits check within
```solidity
        if (maxWhitelistAmount > 0) {
            require(
                contributions[msg.sender] + msg.value <= maxWhitelistAmount,
                "Exceeding maxWhitelistAmount"
            );
```
- Set a limit for None whitelist tier

## [L-01] Expiry and deadline setter logic differ from constructor
### Description
The logic in the constructor expects fund expiry to be greater than fundraising deadline
```solidity
        require(
            _fundExpiry > fundraisingDeadline,
            "_fundExpiry > fundraisingDeadline"
        );
```
In the setters, it is only checked that
```solidity
        require(newFundExpiry > fundExpiry, "Must choose later fund expiry");
```
or
```solidity
        require(
            newFundraisingDeadline > fundraisingDeadline,
            "new fundraising deadline must be > old one"
        );
```
Which could induce that fund expiry can become lower than fundraising deadline, breaking the required statement from constructor inducing different logic.
### Recommendation
Follow a unique logic where it makes more sense that fund expiry must be greater than fundraising deadline

## [L-02] Precision loss can lead to loss of funds when finalizing foundraising
### Summary
The calculations of tokensToMint can lead to users receiving less funds than expected due to a precision issue
### Finding Description
The amount of tokens to mint for a user depends on the user contribution and is computed via the following formula
```solidity
            uint256 tokensToMint = (contribution * SUPPLY_TO_FUNDRAISERS) / totalRaised;
```
SUPPLY_TO_FUNDRAISERS is a constant with a value of `1_000_000_000 * 1e18 = 1e9 * 1e18 = 1e27` If the protocol expects to raise a high amount of token such as 2e30 tokens for example, a "small" contribution relative to this could lead to a mint of 0 tokens. Let's take for example a case where the user contributed 1e3. The result would be `(1e3 * 1e27) / 2e30 = 0.5 = 0` in Solidity.

Note: some ERC20 tokens could also revert on 0 amount of tokens mint which would cause a DOS and make it impossible to finalize a foundraising if at least one user minted an amount as provided in the previous example. A user could purposely mint a very small amount in order to DOS the fundraising finalization.
### Impact Explanation
- Users to receive less minted tokens than expected
- DOS if ERC20 token doesn't allow mint of 0 tokens
### Recommendation
Add a minimum contribution amount coherent compared to the magnitude of totalRaised

## [L-02] whitelist mapping and getter will always return False
### Description
Due to `whitelist` mapping not being implemented anywhere, it doesn't properly follow whitelisted users. When users are whitelisted or removed from whitelist, it doesn't set `whitelist[address]` to True or False. Thus `Daao::whitelist()` will always return False which could be confusing and be a potential issue with off-chain systems
### Proof of Concept
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import {Test} from "forge-std/Test.sol";
import "../src/Daao.sol";

contract DaaoTest is Test {
    Daao public daao;
    address owner = address(0x1);
    address public protocolAdmin = makeAddr("protocolAdmin");
    address public whitelistedUser = makeAddr("whitelistedUser");
    address public nonWhitelistedUser = makeAddr("nonWhitelistedUser");
    uint256 fundraisingGoal = 10 ether;
    uint256 fundraisingDeadline = block.timestamp + 1 weeks;
    uint256 fundExpiry = block.timestamp + 2 weeks;
    uint256 maxWhitelistAmount = 10 ether;
    uint256 maxPublicContributionAmount = 0;

    function setUp() public {
        vm.prank(owner);
        // Deploy the Daao contract
        daao = new Daao(
            fundraisingGoal,
            "TestDAO",
            "TDAO",
            fundraisingDeadline,
            fundExpiry,
            owner,
            address(0x5), // liquidityLockerFactory
            maxWhitelistAmount,
            protocolAdmin,
            maxPublicContributionAmount
        );

        vm.deal(whitelistedUser, 10 ether);

        // Whitelist a user
        address[] memory users = new address[](1);
        users[0] = whitelistedUser;
        Daao.WhitelistTier[] memory tiers = new Daao.WhitelistTier[](1);
        tiers[0] = Daao.WhitelistTier.Gold;

        vm.prank(owner);
        daao.addToWhitelist(users, tiers);
    }

    function testGetWhitelistLengthReturns0WithWhitelistedUsers() public {
        // Contribute as a whitelisted user
        vm.prank(whitelistedUser);
        daao.contribute{value: 0.1 ether}();

        // Verify that the contribution was recorded
        uint256 contribution = daao.contributions(whitelistedUser);
        assertEq(contribution, 0.1 ether);
        assertEq(daao.totalRaised(), 0.1 ether);

        // whitelist() returns False even though the user has just been whitelisted
        assertFalse(daao.whitelist(whitelistedUser));
    }
}
```
### Recommendation
- Remove `whitelist()` public mapping if it isn't planned to be used
- Set whitelist[address] to true when whitelisted
